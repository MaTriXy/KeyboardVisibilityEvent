apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "com.vanniktech.maven.publish"

version = project.properties["VERSION_NAME"]
group = project.properties["GROUP"]

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.30"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0"
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

def getGpgKey() {
    return hasProperty("signingKey") ? signingKey : ""
}

def getGpgPassword() {
    return hasProperty("signingKeyPassword") ? signingKeyPassword : ""
}

mavenPublish {
    releaseSigningEnabled = !getGpgKey().isEmpty() && !getGpgPassword().isEmpty()
    targets {
        testMaven {
            releaseRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
            snapshotRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
        }
    }
}

signing {
    def signingKey = getGpgKey()
    def password = getGpgPassword()
    if (!signingKey.isEmpty() && !password.isEmpty()) {
        useInMemoryPgpKeys(signingKey, password)
    }
}

